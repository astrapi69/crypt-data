
plugins {
    id "java"
    id "maven-publish"
    id "signing"
    id "com.github.ben-manes.versions" version "0.29.0"
    id "com.github.hierynomus.license" version "0.15.0"
}

apply plugin: "jacoco"

group = "de.alpharogroup"
version = "$projectVersion"
description = "crypt-data"
sourceCompatibility = "1.8"

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile "de.alpharogroup:crypt-api:$cryptApiVersion"
    compile "javax.xml.bind:jaxb-api:$jaxbApiVersion"
    compile "commons-codec:commons-codec:$commonsCodecVersion"
    compile "org.bouncycastle:bcpkix-jdk15on:$bcpkixJdk15onVersion"
    compile "com.google.code.findbugs:jsr305:$jsr305Version"
    compile "com.google.guava:guava:$guavaVersion"
    compile "de.alpharogroup:file-worker:$fileWorkerVersion"
    compile "com.rainerhahnekamp:sneakythrow:$sneakythrowVersion"
    compile "org.apache.commons:commons-lang3:$commonsLang3Version"
    implementation("de.alpharogroup:silly-collections:$sillyCollectionsVersion")
    implementation("de.alpharogroup:silly-strings:$sillyStringsVersion")
    implementation("commons-io:commons-io:$commonsIoVersion")
    implementation("de.alpharogroup:silly-io:$sillyIoVersion")
    testCompile "de.alpharogroup:vintage-time:$vintageTimeVersion"
    testCompile "de.alpharogroup:test-objects:$testObjectsVersion"
    testCompile "io.github.benas:random-beans:$randomBeansVersion"
    testCompile "de.alpharogroup:randomizer-core:$randomizerCoreVersion"
    testCompile("de.alpharogroup:checksum-up:$checksumUpVersion")
    testCompile "org.meanbean:meanbean:$meanbeanVersion"
    testCompile "de.alpharogroup:jobj-contract-verifier:$jobjContractVerifierVersion"
    testCompile "org.testng:testng:$testngVersion"
}

license {
    ext.year="2015"
    ext.owner="Asterios Raptis"
    header rootProject.file("src/main/resources/LICENSE.txt")
    excludes(["**/README",
              "**/README.md",
              "**/LICENSE",
              "**/NOTICE",
              "**/*.xml",
              "**/*.xsl",
              "**/*.xsd",
              "**/*.dtd",
              "**/*.html",
              "**/*.jsp",
              "**/*.jpa",
              "**/*.sql",
              "**/*.properties",
              "**/*.bat",
              "**/*.gradle",
              "**/*.MF",
              "**/*.txt",
              "**/*.vm",
              "**/*.log",
              "**/*.map",
              "**/*.js.map",
              "**/*.tmpl",
              "**/*.js.tmpl",
              "**/*.editorconfig",
              "src/test/resources/**",
              "src/main/resources/**",
              "out/**",
              "build/**"])
}

// workhack for license issue #76
gradle.startParameter.excludedTaskNames += "licenseMain"
gradle.startParameter.excludedTaskNames += "licenseTest"

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from(sourceSets.main.allJava)
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set("javadoc")
    javadoc.properties.failOnError = true
}

def releaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "${rootProject.name}"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "${rootProject.name}"
                description = "The target of this project is to provide data beans, writers and readers for encryption and decryption"
                url = "https://github.com/astrapi69/"+"${rootProject.name}"
                organization {
                    name = "Alpha Ro Group UG (haftungsbeschr√§ngt)"
                    url = "http://www.alpharogroup.de/"
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/astrapi69/"+"${rootProject.name}"+"/issues"
                }
                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "astrapi69"
                        name = "Asterios Raptis"
                    }
                }
                scm {
                    connection = "scm:git:git:@github.com:astrapi69/"+"${rootProject.name}"+".git"
                    developerConnection = "scm:git:git@github.com:astrapi69/"+"${rootProject.name}"+".git"
                    url = "git:@github.com:astrapi69/"+"${rootProject.name}"+".git"
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv("ossrhUsername") ?: project.property("ossrhUsername")
                password System.getenv("ossrhPassword") ?: project.property("ossrhPassword")
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = releaseVersion ? releasesRepoUrl : snapshotsRepoUrl
        }
    }
}

signing {
    if (releaseVersion) {
        sign publishing.publications.mavenJava
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport
